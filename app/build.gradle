buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public'}
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

Properties props = new Properties()
try {
    props.load(file('./crashlytics.properties').newDataInputStream())
} catch (Exception ex) {
    throw new GradleException("Missing crashlytics.properties, check the crashlytics.properties file.");
}

Properties localProps = new Properties()
try {
    localProps.load(file('./private.properties').newDataInputStream())
} catch (Exception ex) {
    throw new GradleException("Missing private.properties, check the private.properties file.");
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.tomoima.fetchtweet"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [
                fabricApiKey: "${localProps.getProperty("fabricApiKey")}"   // Set this in your ~/.gradle/gradle.properties or /app/private.properties as: fabricApiKey to use a env variable (and then set that value, of course)
        ]
    }
    buildTypes {
        debug {
            buildConfigField "String", "TWITTER_KEY", "\"${props.getProperty("twitterKey")}\""
            buildConfigField "String", "TWITTER_SECRET", "\"${props.getProperty("twitterSecret")}\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'javax.annotation:jsr250-api:1.0'
    compile('com.twitter.sdk.android:twitter:1.13.0@aar') {
        transitive = true;
    }
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    debugCompile 'com.tomoima.debot:debot:1.1.0'
    releaseCompile 'com.tomoima.debot:debot-no-op:1.1.0'
    compile 'com.jakewharton.timber:timber:4.1.1'
    provided 'org.projectlombok:lombok:1.16.8'
}
